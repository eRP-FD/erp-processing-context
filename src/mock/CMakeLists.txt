#
# (C) Copyright IBM Deutschland GmbH 2021, 2024
# (C) Copyright IBM Corp. 2021, 2024
#
# non-exclusively licensed to gematik GmbH
#

set(TEST_DATA_DIR "${CMAKE_SOURCE_DIR}/resources/test")
set(GENERATED_NOTE "This file has been generated by CMake")
configure_file("${CMAKE_CURRENT_SOURCE_DIR}/mock_config.h.in" "${CMAKE_CURRENT_BINARY_DIR}/mock_config.h")

add_library(erp-hsm-tpm-mock OBJECT)
target_sources(erp-hsm-tpm-mock
    PRIVATE
        crypto/MockCryptography.cxx
        hsm/HsmMockClient.cxx
        hsm/HsmMockFactory.cxx
        hsm/MockBlobCache.cxx
        idp/MockIdpUpdater.cxx
        tpm/TpmMock.cxx
        tpm/TpmTestHelper.cxx
        tsl/MockOcsp.cxx
        tsl/MockTslManager.cxx
        tsl/UrlRequestSenderMock.cxx
        util/MockConfiguration.cxx
        $<$<BOOL:${ERP_WITH_HSM_TPM_PRODUCTION}>:enrolment/MockEnrolmentManager.cxx>
        $<$<BOOL:${ERP_WITH_HSM_TPM_PRODUCTION}>:enrolment/TpmProxyApi.cxx>
    INTERFACE
        $<TARGET_OBJECTS:erp-hsm-tpm-mock>
        $<$<BOOL:${ERP_WITH_HSM_TPM_PRODUCTION}>:$<TARGET_OBJECTS:enrolment-api-client>>
)

find_package(Boost REQUIRED)
find_package(date REQUIRED)
find_package(glog REQUIRED)
find_package(gsl-lite REQUIRED)
find_package(PostgreSQL REQUIRED)
find_package(libpqxx REQUIRED)
find_package(magic_enum REQUIRED)
find_package(RapidJSON REQUIRED)
find_package(libxml2 REQUIRED)

target_link_libraries(erp-hsm-tpm-mock
    PUBLIC
        boost::boost
        date::date
        glog::glog
        gsl::gsl-lite
        PostgreSQL::PostgreSQL
        libpqxx::pqxx
        magic_enum::magic_enum
        openssl::openssl
        rapidjson
        LibXml2::LibXml2
        $<$<BOOL:${ERP_WITH_HSM_TPM_PRODUCTION}>:hsmclient::hsmclient>
        $<$<BOOL:${ERP_WITH_HSM_TPM_PRODUCTION}>:tpmclient::tpmclient>
)


target_compile_definitions(erp-hsm-tpm-mock
    PRIVATE
        ENABLE_DEBUG_LOG
    PUBLIC
        WITH_HSM_MOCK=1
        $<$<BOOL:${ERP_WITH_HSM_TPM_PRODUCTION}>:WITH_HSM_TPM_PRODUCTION=1>
)

target_include_directories(
    erp-hsm-tpm-mock
        PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/.. # Include the erp prefixes in include statements.
                ${CMAKE_CURRENT_BINARY_DIR}
                ${CMAKE_SOURCE_DIR}
)
